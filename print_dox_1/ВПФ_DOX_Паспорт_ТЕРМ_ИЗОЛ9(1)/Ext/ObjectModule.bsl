 

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

Функция СведенияОВнешнейОбработке() Экспорт
	
	ВерсияБСП = "3.0.1.0";
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	ПараметрыРегистрации.Информация = НСтр("ru = 'Договор (Word14)'");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Версия = "1.0.0.0";
	ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказПоставщику");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Договор (Word14)'");
	Команда.Идентификатор = "ПечатьДоговораWord";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
		
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ПечатьДоговораWord");
	Если ПечатнаяФорма <> Неопределено Тогда		
		ОфисныеДокументы = НапечататьДоговор(МассивОбъектов);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Печать договора (Word)'");
		ПечатнаяФорма.ОфисныеДокументы = НапечататьДоговор(МассивОбъектов);
	КонецЕсли;
	
КонецПроцедуры

Функция НапечататьДоговор(МассивОбъектов)
	
	ОфисныеДокументы = Новый Соответствие;
	МакетДокумента = ПолучитьМакет("Макет14");
	Шаблон = НСтр("ru = 'Заказ покупателя №[Номер] от [Дата] [Контрагент]'");
	
	Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(МакетДокумента, Неопределено);
	Если Макет = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ВерхнийКолонтитул",       "ВерхнийКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ВерхнийЧетныйКолонтитул", "ВерхнийЧетныйКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Шапка",         		   "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ШапкаТаблицы",            "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицы",           "СтрокаТаблицы");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал",         		   "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "НижнийКолонтитул",        "НижнийКолонтитул");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "НижнийЧетныйКолонтитул",  "НижнийЧетныйКолонтитул");
	
	ДанныеПоШапке = ПолучитьДанныеДляПечати(МассивОбъектов);
	
	Для Каждого ДанныеПечати Из ДанныеПоШапке Цикл
		
		ДанныеПечати.Дата = Формат(ДанныеПечати.Дата, "ДЛФ=DD");
		

		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);
		
		Если ПечатнаяФорма = Неопределено Тогда
			УправлениеПечатью.ОчиститьСсылки(Макет);
			Возврат ОфисныеДокументы;
		КонецЕсли;
		
		// Вывод верхних колонтитулов
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ВерхнийКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Для четных страниц свои колонтитулы
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ВерхнийЧетныйКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Вывод верхней части документа - обычная область с параметрами.
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Шапка"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Вывод коллекции данных из информационной базы в виде таблицы.
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["ШапкаТаблицы"]);
		УправлениеПечатью.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		
		// Вывод строка ТЧ
		ДанныеПечатиТовары = ОбщегоНазначения.ТаблицаЗначенийВМассив(ДанныеПечати.Ссылка.Товары.Выгрузить());
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["СтрокаТаблицы"]);
		УправлениеПечатью.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеПечатиТовары);

		// Вывод подвала документа
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["Подвал"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Вывод нижних колонтитулов
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["НижнийКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		// Для четных страниц свои колонтитулы
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей["НижнийЧетныйКолонтитул"]);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеПечати);
		
		АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
	
		УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
		
		ЗначенияРеквизитовДокумента = Новый Структура("Контрагент,Номер,Дата,Ссылка");
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитовДокумента, ДанныеПечати);
		ЗначенияРеквизитовДокумента.Дата = Формат(ЗначенияРеквизитовДокумента.Дата, "ДЛФ=D");
		ЗначенияРеквизитовДокумента.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЗначенияРеквизитовДокумента.Номер);
		ИмяДокумента = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ЗначенияРеквизитовДокумента);
		
		ОфисныеДокументы.Вставить(АдресХранилищаПечатнойФормы, ИмяДокумента);
		
	КонецЦикла;
	
	УправлениеПечатью.ОчиститьСсылки(Макет);
	
	Возврат ОфисныеДокументы;
	
КонецФункции 

Функция ПолучитьДанныеДляПечати(МассивОбъектов)
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |   ЗаказПоставщику.Ссылка КАК Ссылка,
    |   ЗаказПоставщику.Номер КАК Номер,
    |   ЗаказПоставщику.Дата КАК Дата,
    |   ЗаказПоставщику.Контрагент КАК Контрагент,
    |   ЗаказПоставщику.Контрагент.Наименование КАК КонтрагентНаименование
    |ИЗ
    |   Документ.ЗаказПоставщику КАК ЗаказПоставщику
    |ГДЕ
    |   ЗаказПоставщику.Ссылка В(&МассивСсылок)";
    
    Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
    
    Результат = Запрос.Выполнить();
    ТаблицаДанных = Результат.Выгрузить();
    
    // Преобразуем в массив структур
    МассивДанных = Новый Массив;
    Для Каждого Строка Из ТаблицаДанных Цикл
        СтруктураДанных = Новый Структура;
        СтруктураДанных.Вставить("Ссылка", Строка.Ссылка);
        СтруктураДанных.Вставить("Номер", Строка.Номер);
        СтруктураДанных.Вставить("Дата", Строка.Дата);
        СтруктураДанных.Вставить("Контрагент", Строка.КонтрагентНаименование);
        МассивДанных.Добавить(СтруктураДанных);
    КонецЦикла;
    
    Возврат МассивДанных;
КонецФункции
#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли